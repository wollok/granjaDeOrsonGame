import wollok.game.*
import granjero.*
import arbustos.*
import granero.*
import cultivos.*
import elementos.*
import nivel.*
import factories.*

program FarmvilleGame {
//	CONFIG	
	wgame.setTitle("FarmVille")
	wgame.setHeight(12)
	wgame.setWidth(12)
	wgame.setGround("ground.png")
	
//	ARBUSTOS
	val ancho = wgame.getWidth() - 1
	val largo = wgame.getHeight() - 1
	
	(1 .. ancho-1).forEach { n => arbustoFactory.draw(new ArbustoAbajo(),new Position(n, 0)) } // bordeAbajo
	(1 .. ancho-1).forEach { n => arbustoFactory.draw(new ArbustoArriba(), new Position(n, largo)) } // bordeArriba 
	(0 .. largo).forEach { n => arbustoFactory.draw(new ArbustoIzquierda(), new Position(0, n)) } // bordeIzq 
	(0 .. largo).forEach { n => arbustoFactory.draw(new ArbustoDerecha(), new Position(ancho, n)) } // bordeDer

	arbustoFactory.drawVerticalBlock(5, 5)
	[1, 9].forEach{ n => 
		arbustoFactory.drawVerticalBlock(5, n)
		arbustoFactory.drawHorizontalBlock(n, 5)
	}
	
	nivel.configure()
	
//	VISUALES
 	wgame.addVisualCharacter(granjero)

	new Position(9,9).drawElement(granero)
	wgame.whenCollideDo(granero, { g =>  if (granjero == g) granero.hazLoTuyo() })

	
	[gorra, espada].forEach { it => 
		wgame.addVisual(it)
		wgame.whenCollideDo(it, { g =>
			if (granjero == g)
				if (!(granjero.posicion() === it.posicion()))
					granjero.agarrar(it)
		})
	}
	
	var maiz = new Maiz()
	maiz.crece()
	granjero.agregarCultivo(maiz)
	new Position(9,2).drawElement(maiz)

//	TECLADO
	A.onPressCharacterSay { "Tengo " + granjero.oro() + " monedas de oro" }
	SPACE.onPressDo { granjero.usarElemento() }
	M.onPressDo { cultivoFactory.cultiva(new Maiz()) }
	T.onPressDo { cultivoFactory.cultiva(new Trigo()) }
	O.onPressDo { cultivoFactory.cultiva(new Tomaco()) }
	R.onPressDo { granjero.rega() }
	
//	START
	wgame.start()
}
